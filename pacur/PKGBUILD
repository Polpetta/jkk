targets=(
    "archlinux"
    "fedora"
    "opensuse"
    "ubuntu"
)
pkgname="jkk"
pkgver="0.1"
pkgrel="1"
pkgdesc="A git-like cli for Jenkins written in Kotlin"
pkgdesclong=("A git-like cli for Jenkins written in Kotlin")
maintainer="JKK Team"
url="https://github.com/Polpetta/jkk"
arch="all"
license=("GPL3")
section="utils"
priority="optional"
sources=(
    "https://github.com/Polpetta/jkk/archive/master.tar.gz"
    "https://downloads.gradle-dn.com/distributions/gradle-6.6.1-bin.zip"
)
sources:archlinux=("https://github.com/Polpetta/jkk/archive/master.tar.gz")

hashsums=(
    "skip"
    "7873ed5287f47ca03549ab8dcb6dc877ac7f0e3d7b1eb12685161d10080910ac"
)
hashsums:archlinux=(
    "skip"
)

depends:apt=(
    "openjdk-11-jre-headless"
)
makedepends:apt=(
    "openjdk-11-jre-headless"
)

depends:pacman=(
    "java-runtime"
)
makedepends:pacman=(
    "gradle"
    "unzip"
)

depends:yum=(
    "java-11-openjdk-headless"
)
makedepends:yum=(
    "java-11-openjdk-devel"
)

depends:zypper=(
    "java-11-openjdk-headless"
)
makedepends:zypper=(
    "java-11-openjdk-devel"
)

build:archlinux() {
    export JAVA_HOME=/usr/lib/jvm/default/
    cd "${srcdir}/${pkgname}-master"
    gradle install
}

build:fedora() {
    export JAVA_HOME=/usr/lib/jvm/java-11-openjdk/
    cd "${srcdir}/${pkgname}-master"
    ../gradle-6.6.1/bin/gradle install
}

build:opensuse() {
    export JAVA_HOME=/usr/lib64/jvm/java-11-openjdk/
    cd "${srcdir}/${pkgname}-master"
    ../gradle-6.6.1/bin/gradle install
}

build:ubuntu() {
    export JAVA_HOME=/usr/lib/jvm/java-11-openjdk-amd64
    cd "${srcdir}/${pkgname}-master"
    ../gradle-6.6.1/bin/gradle install
}

package() {
    cd "${srcdir}/${pkgname}-master"/build/
    
    install -Dm 755 install/${pkgname}/bin/jkk \
        -t "${pkgdir}/usr/share/java/${pkgname}/bin"
    install -Dm 644 install/${pkgname}/lib/* \
        -t "${pkgdir}/usr/share/java/${pkgname}/lib"
    install -d "${pkgdir}/usr/bin"
    ln -s /usr/share/java/${pkgname}/bin/jkk \
        "${pkgdir}/usr/bin/jkk"
    
    install -Dm 644 ../LICENSE \
        -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm 644 ../README.md \
        -t "${pkgdir}/usr/share/doc/${pkgname}"
}
